# - Try to find SPIRV-CROSS-C
# Once done this will define:
#   SPIRV_CROSS_C_FOUND
#   SPIRV_CROSS_C_INCLUDE_DIR
#   SPIRV_CROSS_C_LIBRARIES
#   SPIRV_CROSS_C::spirv-cross-c

find_path(
  SPIRV_CROSS_C_INCLUDE_DIR
  spirv_cross/spirv_cross_c.h
  PATHS ${SPIRV_CROSS_C_PATH_INCLUDES}
)

# Static components
find_library(SPIRV_CROSS_C_LIB_CORE  NAMES spirv-cross-core PATHS ${SPIRV_CROSS_C_PATH_LIB} ${ADDITIONAL_LIBRARY_PATHS})
find_library(SPIRV_CROSS_C_LIB_GLSL  NAMES spirv-cross-glsl PATHS ${SPIRV_CROSS_C_PATH_LIB} ${ADDITIONAL_LIBRARY_PATHS})
find_library(SPIRV_CROSS_C_LIB_UTIL  NAMES spirv-cross-util PATHS ${SPIRV_CROSS_C_PATH_LIB} ${ADDITIONAL_LIBRARY_PATHS})
find_library(SPIRV_CROSS_C_LIB_C     NAMES spirv-cross-c    PATHS ${SPIRV_CROSS_C_PATH_LIB} ${ADDITIONAL_LIBRARY_PATHS})

# Shared fallback
find_library(SPIRV_CROSS_C_LIB_SHARED NAMES spirv-cross-c-shared spirv-cross-c PATHS ${SPIRV_CROSS_C_PATH_LIB} ${ADDITIONAL_LIBRARY_PATHS})

# Choose linking strategy
set(SPIRV_CROSS_C_LIBRARIES "")
if(SPIRV_CROSS_C_LIB_CORE AND SPIRV_CROSS_C_LIB_GLSL AND SPIRV_CROSS_C_LIB_UTIL AND SPIRV_CROSS_C_LIB_C)
  set(SPIRV_CROSS_C_LIBRARIES
    ${SPIRV_CROSS_C_LIB_CORE}
    ${SPIRV_CROSS_C_LIB_GLSL}
    ${SPIRV_CROSS_C_LIB_UTIL}
    ${SPIRV_CROSS_C_LIB_C}
  )
  set(SPIRV_CROSS_C_IMPORTED_LOCATION "${SPIRV_CROSS_C_LIB_C}")
elseif(SPIRV_CROSS_C_LIB_SHARED)
  set(SPIRV_CROSS_C_LIBRARIES "${SPIRV_CROSS_C_LIB_SHARED}")
  set(SPIRV_CROSS_C_IMPORTED_LOCATION "${SPIRV_CROSS_C_LIB_SHARED}")
endif()

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(SPIRV_CROSS_C DEFAULT_MSG
  SPIRV_CROSS_C_LIBRARIES SPIRV_CROSS_C_INCLUDE_DIR
)

if(SPIRV_CROSS_C_FOUND)
  add_library(SPIRV_CROSS_C::spirv-cross-c UNKNOWN IMPORTED)
  set_target_properties(SPIRV_CROSS_C::spirv-cross-c PROPERTIES
    IMPORTED_LOCATION "${SPIRV_CROSS_C_IMPORTED_LOCATION}"
    INTERFACE_INCLUDE_DIRECTORIES "${SPIRV_CROSS_C_INCLUDE_DIR}"
    INTERFACE_LINK_LIBRARIES "${SPIRV_CROSS_C_LIBRARIES}"
  )
endif()

mark_as_advanced(
  SPIRV_CROSS_C_INCLUDE_DIR
  SPIRV_CROSS_C_LIB_CORE
  SPIRV_CROSS_C_LIB_GLSL
  SPIRV_CROSS_C_LIB_UTIL
  SPIRV_CROSS_C_LIB_C
  SPIRV_CROSS_C_LIB_SHARED
)
